{
  ArdyThievHelper
  Author: ineedbot
  Date: 5/25/2022

  Pickpockets ardy knights until 200m exp.

  Use resizable modern layout, and use the mouse over top right of inventory. Uses ESC button to toggle inventory.
}

program ArdyThievHelper;
{$DEFINE SRL_USE_REMOTEINPUT}
{$I SRL/OSR.simba}
{$i Utils/Utils.simba}

var
  uptxt: String;
  tab: ERSGametab;
  ClickTimer, InvOpenTimer, InvCloseTimer, MouseInvMoveTimer, FullBtnTimer, MouseMoveTimer: TCountDown;
  coinpouch: TRSItem;
  fooditems: TRSItemArray;
  slots: TIntegerArray;
  B: TBox;
  i: Int32;

function TScript.ShouldPressKeyboard(): Boolean; override;
begin
  // always use keyboard
  Result := True;
end;

function TRSGameTabs.Open(Tab: ERSGameTab): Boolean; override;
var
  Key: Int32 = -1;
begin
  if Bank.IsOpen(False) then
  begin
    if (Tab = ERSGameTab.INVENTORY) then
      exit(True);

    exit(False);
  end;

  if BankPin.IsOpen() then
    exit(False);

  if Self.GetCurrentTab() = Tab then
    exit(True);

  if Script.ShouldPressKeyboard() then
  begin
    case Tab of
      ERSGameTab.COMBAT: Key := GAMETAB_KEY_COMBAT;
      ERSGameTab.STATS: Key := GAMETAB_KEY_STATS;
      ERSGameTab.QUESTS: Key := GAMETAB_KEY_QUESTS;
      ERSGameTab.INVENTORY: Key := GAMETAB_KEY_INVENTORY;
      ERSGameTab.EQUIPMENT: Key := GAMETAB_KEY_EQUIPMENT;
      ERSGameTab.PRAYER: Key := GAMETAB_KEY_PRAYER;
      ERSGameTab.MAGIC: Key := GAMETAB_KEY_MAGIC;
      ERSGameTab.FRIENDS: Key := GAMETAB_KEY_FRIENDS;
      ERSGameTab.ACCOUNT: Key := GAMETAB_KEY_ACCOUNT;
      ERSGameTab.LOGOUT: Key := GAMETAB_KEY_LOGOUT;
      ERSGameTab.OPTIONS: Key := GAMETAB_KEY_OPTIONS;
      ERSGameTab.EMOTES: Key := GAMETAB_KEY_EMOTES;
      ERSGameTab.CLAN: Key := GAMETAB_KEY_CLAN;
      ERSGameTab.MUSIC: Key := GAMETAB_KEY_MUSIC;
      else Key := -1;
    end;
  end;

  if Key <> -1 then
  begin
    Keyboard.PressKey(Key);
    Wait(0, 500, wdLeft);
  end;

  Result := (Self.GetCurrentTab() = Tab);
end;

begin
  // general setup
  Login.AddPlayer('', '');
  Script.AntiBanEnergy.SetupBiometrics();
  Script.AntiBanEnergy.BoxPTRad := 0.3;
  Script.AntiBan.Name := 'Antiban';
  Script.SleepRate := 50;
  Mouse.Speed             := 17 + Round(BioRandomFixed() * 10) + Random(-2, 2);
  Script.BaseMouseSpeed := Mouse.Speed;
  Script.MaxTiredMouseSpeed := 10;
  MainScreen.UpTextFilter := TOCRAnyColorRule.Create(85, 75);

  // init
  ClickTimer.Init(1);
  InvCloseTimer.Init(1);
  InvOpenTimer.Init(1);
  MouseInvMoveTimer.Init(1);
  FullBtnTimer.Init(1);
  MouseMoveTimer.Init(1);
  coinpouch := 'Coin pouch';
  fooditems := ['Trout'];

  B := Inventory.GetSlotBox(0);

  // setup our mouse
  Mouse.Move(B, True);

  // forever
  while True do
  begin
    Wait(Script.MainLoopWaitAmount(False));
    Script.UpdateMouseSpeed();

    // make sure logged in
    if not RSClient.IsLoggedIn() then
      continue;

    // do we need to eat?
    if Minimap.GetHPLevel() <= 3 then
    begin
      // open inv and check
      GameTabs.Open(ERSGameTab.INVENTORY);

      slots.Clear();

      // no food left!
      if not Inventory.FindItems(fooditems, slots) then
      begin
        PlaySound({$MACRO CURRENT_DIRECTORY} + '\win8not.wav');
        Wait(15000);
      end
      else
      begin
        // eat!
        i := slots[Random(High(slots))];
        Inventory.HoverSlot(i);
        if MainScreen.IsUpText(['Eat', 'Drink']) and (Random() < 0.9) then
          Mouse.Click(MOUSE_LEFT)
        else
          ChooseOption.Select(['Drink', 'Eat']);

        Wait(1250, 1750, wdLeft);
        MouseInvMoveTimer.Restart(1);
      end;

      continue;
    end;

    // gather some info
    uptxt := MainScreen.GetUpText().Strip();
    tab := GameTabs.GetCurrentTab();

    // make sure that our setup is okay
    if tab = ERSGameTab.INVENTORY then
    begin
      slots.Clear();
      if Inventory.FindItem(coinpouch, slots) then
      begin
        if slots[0] <> 0 then
        begin
          continue;
        end;
      end
      else if Inventory.IsSlotUsed(0) then
        continue;
    end
    else if (tab <> ERSGameTab.UNKNOWN) and InvOpenTimer.IsFinished() then
      GameTabs.Open(ERSGameTab.INVENTORY);

    // make sure our mouse is in posistion
    // check uptxt
    if not Mouse.Position().InBox(B) or
      not (uptxt = '') and not ('Pickpocket Knight' in uptxt) and not ('Open-' in uptxt) or
      (uptxt = '') and (tab <> ERSGameTab.INVENTORY) then
    begin
      if MouseInvMoveTimer.IsFinished() then
      begin
        Mouse.Move(B, True);
        MouseInvMoveTimer.Restart(30000);
      end;

      continue;
    end
    else
      MouseInvMoveTimer.Restart(30000);

    // wiggle our mouse
    if MouseMoveTimer.IsFinished() then
    begin
      Mouse.Move(Mouse.Position().Random(-4, 4, True));

      Wait(SRL.SkewedRand(100, 0, 1000));

      if not Mouse.Position().InBox(B) then
        Mouse.Move(B);

      if Random() < 0.95 then
        MouseMoveTimer.Restart(SRL.SkewedRand(1000 * 60 * 20, 1000 * 60 * 15, 1000 * 60 * 30))
      else if Random() < 0.8 then
        MouseMoveTimer.Restart(SRL.SkewedRand(1000 * 60 * 5, 1000 * 60 * 2, 1000 * 60 * 8))
      else
        MouseMoveTimer.Restart(SRL.SkewedRand(1000 * 60 * 10, 1000 * 60 * 1, 1000 * 60 * 50));
    end;

    // click
    if ClickTimer.IsFinished() then
    begin
      Mouse.Click(MOUSE_LEFT);

      if Random() < 0.95 then
        ClickTimer.Restart(SRL.SkewedRand(100, 50, 200))
      else if Random() < 0.9 then
        ClickTimer.Restart(SRL.SkewedRand(500, 50, 1000))
      else
        ClickTimer.Restart(SRL.SkewedRand(1000, 50, 10000));
    end;

    // open/close inv
    if tab = ERSGameTab.INVENTORY then
    begin
      if InvCloseTimer.IsFinished() then
      begin
        // we want to close the inventory, the gametabs call wont allow us to do this
        Keyboard.PressKey(GAMETAB_KEY_INVENTORY);

        InvCloseTimer.Restart(1000);
      end;
    end
    else
    begin
      if InvOpenTimer.IsFinished() then
      begin
        GameTabs.Open(ERSGameTab.INVENTORY);

        if Random() < 0.95 then
          InvOpenTimer.Restart(SRL.SkewedRand(25000, 15000, 30000))
        else if Random() < 0.8 then
          InvOpenTimer.Restart(SRL.SkewedRand(35000, 30000, 40000))
        else
          InvOpenTimer.Restart(SRL.SkewedRand(1500, 1000, 10000));

        if Random() < 0.95 then
          InvCloseTimer.Restart(SRL.SkewedRand(1000, 500, 1500))
        else if Random() < 0.8 then
          InvCloseTimer.Restart(SRL.SkewedRand(2000, 1000, 4000))
        else
          InvCloseTimer.Restart(SRL.SkewedRand(1500, 50, 10000));
      end
      else if ('You need to empty' in Chat.GetMessage(7, [CHAT_COLOR_BLACK])) and FullBtnTimer.IsFinished() then
      begin
        GameTabs.Open(ERSGameTab.INVENTORY);
        FullBtnTimer.Restart(SRL.SkewedRand(30000, 50, 40000));
      end;
    end;
  end;
end.
