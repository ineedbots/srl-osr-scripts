{
  BankStander transform
  Author: ineedbot
  Date: 7/9/2022

  Transform
}

{$IFNDEF INEEDBOT_BANKSTANDER_TRANSFORM}
{$DEFINE INEEDBOT_BANKSTANDER_TRANSFORM}

{$IFNDEF SRL_OSR}
  {$I SRL/OSR.simba}
{$ENDIF}

{$include_once ../Utils/Utils.simba}

{$include_once Types.simba}
{$include_once Utils.simba}

{
  Transforms the item with a tool
}
function TBot.Transform(Item: TRSItem; MaterialSlots, ToolSlots: TIntegerArray): Boolean;
var
  ToolSlot, MaterialSlot, MCount, MC, StopShort, OGMCount: Int32;
  Made: Boolean;
  CountDown: TCountDown;
begin
  // can be secondary material too!
  ToolSlot := ToolSlots[Random(High(ToolSlots))];
  MaterialSlot := MaterialSlots[Random(High(MaterialSlots))];

  MCount := Length(MaterialSlots);
  OGMCount := MCount;

  StopShort := MCount;
  if Random() < 0.05 then
    StopShort := SRL.SkewedRand(MCount, MCount, 0);

  Script.WriteMsg('Starting the transformation', True);

  // start transforming
  if Random() < 0.5 then
  begin
    Inventory.SetSelectedSlot(ToolSlot);
    Wait(0, 1000, wdLeft);
    Inventory.ClickSlot(MaterialSlot);
  end
  else
  begin
    Inventory.SetSelectedSlot(MaterialSlot);
    Wait(0, 1000, wdLeft);
    Inventory.ClickSlot(ToolSlot);
  end;

  if Self.MakeNum = -1 then
    Made := True;

  if not Made and Make.IsOpen(SRL.SkewedRand(1500, 1250, 2500)) then
  begin
    Wait(0, 3333, wdLeft);
    if Length(Make.GetItemButtons()) > Self.MakeNum then
      Made := Make.Select(Self.MakeNum, MAKE_QUANTITY_ALL, Script.ShouldPressKeyboard());
  end;

  if not Made then
  begin
    Script.WriteMsg('Failed to open make', True);
    exit();
  end;

  Result := True;

  Self.AfterActionAntiBan(True, False);
  CountDown.Init(Self.TransformWaitTime);

  Script.WriteMsg('Making...', True);

  while RSClient.IsLoggedIn() do
  begin
    if Self.HandleAmuletOfChem() then
    begin
      Script.WriteMsg('Transform: HandleAmuletOfChem!', True);
      break;
    end;

    // check inv
    MaterialSlots.Clear();
    Inventory.FindItem(Item, MaterialSlots);
    MC := Length(MaterialSlots);

    if MC < Self.MaterialRatio then
    begin
      Script.WriteMsg('Transform: No more material', True);
      break;
    end;

    if MC <> MCount then
    begin
      Script.WriteMsg('Transform: InvCount changed, restarting countdown', True);
      CountDown.Restart();
    end;

    if (StopShort - (OGMCount - MC)) <= 0 then
    begin
      if Random() < 0.65 then
        Self.StoppedShort := True;

      Script.WriteMsg('Stopping short for antiban', True);
      break;
    end;

    MCount := MC;

    if CountDown.IsFinished() then
    begin
      Script.WriteMsg('Transform: Countdown expired', True);
      break;
    end;

    if Chat.LeveledUp() then
    begin
      Script.WriteMsg('Transform: Leveld', True);
      if Random() < 0.2 then
      begin
        Wait(0, 1500, wdLeft);

        while Chat.ClickContinue(Script.ShouldPressKeyboard()) do
          Wait(75, 500);
      end;
      break;
    end;

    if not Self.ShouldMakeProduct() then
    begin
      Script.WriteMsg('Transform: shouldn''t make product', True);
      break;
    end;

    Self.DoMainLoopStuff('Transforming items...', True);
  end;

  Wait(0, 5555, wdLeft);
end;

{
  Does transform type
}
function TBot.DoTransform(): Boolean;
var
  MaterialSlots, ToolSlots: TIntegerArray;
  Item: TRSItem;
begin
  if not Inventory.FindItems(Self.ToolItems, ToolSlots) then
  begin
    Script.WriteMsg('No tools', True);
    exit();
  end;

  Script.WriteMsg('DoTransform: ToolSlots: ' + ToStr(ToolSlots), True);

  Script.WriteMsg('Transforming items...', True);

  for Item in Self.MaterialItems do
  begin
    if Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('DoTransform: MaterialSlots: ' + ToStr(MaterialSlots), True);
      Result := Self.Transform(Item, MaterialSlots, ToolSlots);
      break;
    end;
  end;

  Script.WriteMsg('No materials', True);
end;

{
  Combines
}
function TBot.DoCombine(): Boolean;
var
  MaterialSlots, SecondaryMaterialSlots: TIntegerArray;
  Item: TRSItem;
begin
  if not Inventory.FindItems(Self.SecondaryMaterialItems, SecondaryMaterialSlots) then
  begin
    Script.WriteMsg('No secondary materials', True);
    exit();
  end;

  Script.WriteMsg('DoCombine: SecondaryMaterialSlots: ' + ToStr(SecondaryMaterialSlots), True);

  Script.WriteMsg('Combining items...', True);

  for Item in Self.MaterialItems do
  begin
    if Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('DoCombine: MaterialSlots: ' + ToStr(MaterialSlots), True);
      Result := Self.Transform(Item, MaterialSlots, SecondaryMaterialSlots);
      break;
    end;
  end;

  Script.WriteMsg('No materials', True);
end;

{
  Cleans
}
function TBot.Clean(Item: TRSItem; MaterialSlots: TIntegerArray): Boolean;
var
  Pattern: TIntegerArray;
  MCount, MC, StopShort, OGMCount, i, S: Int32;
  CountDown: TCountDown;
  UpTxt: String;
  Clicked, Afk: Boolean;
begin
  S := Mouse.Speed;
  Mouse.Speed += Script.FastMouseAdd + Random(-2, 2);

  Pattern := Bank.GetGoodPattern();

  MCount := Length(MaterialSlots);
  OGMCount := MCount;

  StopShort := MCount;
  if Random() < 0.05 then
    StopShort := SRL.SkewedRand(MCount, MCount, 0);

  if Random() < 0.05 then
    Afk := True;

  while RSClient.IsLoggedIn() do
  begin
    // make sure nothing is selected
    Inventory.SetSelectedSlot(-1);

    // check for items
    MaterialSlots.Clear();
    if not Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('Clean: No items for cleaning found', True);
      break;
    end;

    // clean up the pattern!
    for i := High(Pattern) downto Low(Pattern) do
      if MaterialSlots.Find(Pattern[i]) = -1 then
        Pattern.Remove(Pattern[i]);

    // make sure!
    if Length(Pattern) <= 0 then
    begin
      Script.WriteMsg('Clean: Pattern is empty', True);
      break;
    end;

    // pop!
    i := Pattern[0];
    Pattern.Remove(i);

    // click it!
    Mouse.Move(Inventory.GetSlotBox(i));

    UpTxt := MainScreen.GetUpText();
    if (UpTxt.Count('Clean') > 0) and (Random() < 0.975) then
    begin
      Mouse.Click(MOUSE_LEFT);
      Clicked := True;
    end
    else
      Clicked := ChooseOption.Select('Clean');

    if not Clicked then
    begin
      Script.WriteMsg('Didnt click the clean, trying another', True);
      continue;
    end;

    Result := True;

    // start afking?
    if Random() < (0.05 / 28) then
      Afk := True;

    CountDown.Init(Self.TransformWaitTime);
    while RSClient.IsLoggedIn() do
    begin
      // check inv
      MC := Length(MaterialSlots);

      // check if stopping short
      if (StopShort - (OGMCount - MC)) <= 0 then
      begin
        if Random() < 0.65 then
          Self.StoppedShort := True;

        Script.WriteMsg('Stopping short for antiban', True);
        break;
      end;

      // stop afking?
      if Random() < (0.05 / 14) then
        Afk := False;

      if not Afk then
        break;

      if MC < Self.MaterialRatio then
      begin
        Script.WriteMsg('Clean: No more material', True);
        break;
      end;

      if MC <> MCount then
      begin
        Script.WriteMsg('Clean: InvCount changed, restarting countdown', True);
        CountDown.Restart();
      end;

      MCount := MC;

      if CountDown.IsFinished() then
      begin
        Script.WriteMsg('Clean: Countdown expired', True);
        break;
      end;

      if Chat.LeveledUp() then
      begin
        Script.WriteMsg('Clean: Leveld', True);
        if Random() < 0.2 then
        begin
          Wait(0, 1500, wdLeft);

          while Chat.ClickContinue(Script.ShouldPressKeyboard()) do
            Wait(75, 500);
        end;
        break;
      end;

      if not Self.ShouldMakeProduct() then
      begin
        Script.WriteMsg('Clean: shouldn''t make product', True);
        break;
      end;

      Self.DoMainLoopStuff('Cleaning items...', True);

      // update!
      MaterialSlots.Clear();
      Inventory.FindItem(Item, MaterialSlots);
    end;

    if MC < Self.MaterialRatio then
    begin
      Script.WriteMsg('Clean: No more material', True);
      break;
    end;

    if Self.StoppedShort then
    begin
      Script.WriteMsg('Clean: stopping short', True);
      break;
    end;
  end;

  Mouse.Speed := S;
  Wait(0, 5555, wdLeft);
end;

{
  Does clean type
}
function TBot.DoClean(): Boolean;
var
  MaterialSlots: TIntegerArray;
  Item: TRSItem;
begin
  Script.WriteMsg('Cleaning items...', True);

  for Item in Self.MaterialItems do
  begin
    if Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('DoClean: MaterialSlots: ' + ToStr(MaterialSlots), True);
      Result := Self.Clean(Item, MaterialSlots);
      break;
    end;
  end;

  Script.WriteMsg('No materials', True);
end;

{
  Does the enchanting
}
function TBot.Enchant(Item: TRSItem; MaterialSlots: TIntegerArray): Boolean;
var        
  Pattern: TIntegerArray;
  B: TBox;
  MCount, OGMCount, StopShort, i, MC: Int32;
  CountDown: TCountDown;    
  Afk: Boolean;
begin
  Pattern := Bank.GetGoodPattern();

  MCount := Length(MaterialSlots);
  OGMCount := MCount;

  StopShort := MCount;
  if Random() < 0.05 then
    StopShort := SRL.SkewedRand(MCount, MCount, 0);

  if Random() < 0.05 then
    Afk := True;

  while RSClient.IsLoggedIn() do
  begin
    // open magic book
    if not Magic.Open() then
    begin
      Script.WriteMsg('Failed to open magic', True);
      break;
    end;

    if not Magic.GetSpellBox(Self.Spell, B) then
    begin
      Script.WriteMsg('Failed to GetSpellBox', True);
      break;
    end;

    // start the casts!
    Script.WriteMsg('Casting spell', True);
    Wait(0, 1250, wdLeft);
    Mouse.Click(B, MOUSE_LEFT);

    // make sure the inv opens
    CountDown.Init(SRL.SkewedRand(2000, 1500, 2500));
    while RSClient.IsLoggedIn() do
    begin
      if Inventory.IsOpen() then
        break;

      if CountDown.IsFinished() then
        break;

      Self.DoMainLoopStuff('Waiting for inv to open', False);
    end;

    if not Inventory.IsOpen() then
    begin
      Script.WriteMsg('Inv didnt open', True);
      break;
    end;

    Script.WriteMsg('Inv opened', True); 
    Wait(0, 1250, wdLeft);

    if not Self.ShouldMakeProduct() then
    begin
      Script.WriteMsg('Shouldnt make product', True);
      break;
    end;

    // make sure theres mats
    if not Inventory.FindItems(Self.ToolItems) then
    begin
      Script.WriteMsg('No runes found!', True);
      break;
    end;

    // get mats
    MaterialSlots.Clear();
    if not Inventory.FindItem(Item, MaterialSlots) or (Length(MaterialSlots) < Self.MaterialRatio) then
    begin
      Script.WriteMsg('No materials found!', True);
      break;
    end;

    // update pattern
    for i := High(Pattern) downto Low(Pattern) do
      if MaterialSlots.Find(Pattern[i]) = -1 then
        Pattern.Remove(Pattern[i]);

    if Length(Pattern) <= 0 then
    begin
      Script.WriteMsg('Emmpty pattern!', True);
      break;
    end;

    // pop!
    i := Pattern[0];
    Pattern.Remove(i);

    // start enchanting

    // make sure that we are casting
    Mouse.Move(Inventory.GetSlotBox(i));
    if not MainScreen.IsUpText('->') then
    begin
      Script.WriteMsg('Not casting!', True);
      break;
    end;

    Script.WriteMsg('Enchanting item', True);
    Mouse.Click(Inventory.GetSlotBox(i), MOUSE_LEFT);

    // move our mouse to the spot again
    if Random() < 0.8 then
    begin
      Wait(0, 1250, wdLeft);
      Mouse.Move(B.Expand(10));
    end;

    // wait for enchant, we should swap back to the magic tab when it begins
    CountDown.Init(SRL.SkewedRand(4000, 3500, 6000));
    while RSClient.IsLoggedIn() do
    begin
      if Magic.IsOpen() then
        break;

      if CountDown.IsFinished() then
        break;

      Self.DoMainLoopStuff('Waiting for magic to open', False);
    end;

    if not Magic.IsOpen() then
    begin
      Script.WriteMsg('Magic didnt reopen', True);
      break;
    end;

    Result := True;
    Script.WriteMsg('Magic opened', True);

    // start afking?
    if Random() < (0.05 / 28) then
      Afk := True;

    CountDown.Init(Self.TransformWaitTime);
    while RSClient.IsLoggedIn() do
    begin
      // check inv
      MC := Length(MaterialSlots);

      // check if stopping short
      if (StopShort - (OGMCount - MC)) <= 0 then
      begin
        if Random() < 0.65 then
          Self.StoppedShort := True;

        Script.WriteMsg('Stopping short for antiban', True);
        break;
      end;

      // stop afking?
      if Random() < (0.05 / 14) then
        Afk := False;

      if not Afk then
        break;

      if MC < Self.MaterialRatio then
      begin
        Script.WriteMsg('Enchant: No more material', True);
        break;
      end;

      if MC <> MCount then
      begin
        Script.WriteMsg('Enchant: InvCount changed, restarting countdown', True);
        CountDown.Restart();
      end;

      MCount := MC;

      if CountDown.IsFinished() then
      begin
        Script.WriteMsg('Enchant: Countdown expired', True);
        break;
      end;

      if Chat.LeveledUp() then
      begin
        Script.WriteMsg('Enchant: Leveld', True);
        if Random() < 0.2 then
        begin
          Wait(0, 1500, wdLeft);

          while Chat.ClickContinue(Script.ShouldPressKeyboard()) do
            Wait(75, 500);
        end;
        break;
      end;

      if not Self.ShouldMakeProduct() then
      begin
        Script.WriteMsg('Enchant: shouldn''t make product', True);
        break;
      end;

      Self.DoMainLoopStuff('Enchanting items...', True);

      // update!
      MaterialSlots.Clear();
      Inventory.FindItem(Item, MaterialSlots);
    end;

    Wait(0, 3000, wdLeft);

    if MC < Self.MaterialRatio then
    begin
      Script.WriteMsg('Enchant: No more material', True);
      break;
    end;

    if Self.StoppedShort then
    begin
      Script.WriteMsg('Enchant: stopping short', True);
      break;
    end;

    if Length(Pattern) <= 0 then
    begin
      Script.WriteMsg('Enchant: Pattern is compelte', True);
      break;
    end;
  end;

  Wait(0, 5555, wdLeft);
end;

{
  Enchants!
}
function TBot.DoEnchant(): Boolean;
var
  MaterialSlots: TIntegerArray;
  Item: TRSItem;
begin
  // runes are tools!
  if not Inventory.FindItems(Self.ToolItems) then
  begin
    Script.WriteMsg('No runes', True);
    exit();
  end;

  Script.WriteMsg('Enchanting items...', True);

  for Item in Self.MaterialItems do
  begin
    if Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('DoEnchant: MaterialSlots: ' + ToStr(MaterialSlots), True);
      Result := Self.Enchant(Item, MaterialSlots);
      break;
    end;
  end;

  Script.WriteMsg('No materials', True);
end;

{
  Activates the glassmake!
}
function TBot.DoGlassMake(): Boolean;
var
  B: TBox;
begin
  // open magic book
  if not Magic.Open() then
  begin
    Script.WriteMsg('Failed to open magic', True);
    exit();
  end;

  if not Magic.GetSpellBox(Self.Spell, B) then
  begin
    Script.WriteMsg('Failed to GetSpellBox', True);
    exit();
  end;

  // start the casts!
  Script.WriteMsg('Casting spell', True);
  Wait(0, 1250, wdLeft);
  Mouse.Click(B, MOUSE_LEFT);

  // hover next action
  if Random() < 0.8 then
    Mouse.Move(MainScreen.GetPlayerBox());

  // wait for the operation to complete!!
  Wait(SRL.SkewedRand(4000, 3000, 5000));

  Result := True;
end;

{$ENDIF} // INEEDBOT_BANKSTANDER_TRANSFORM
