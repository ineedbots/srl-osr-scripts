{
  BankStander transform
  Author: ineedbot
  Date: 7/9/2022

  Transform
}

{$IFNDEF INEEDBOT_BANKSTANDER_TRANSFORM}
{$DEFINE INEEDBOT_BANKSTANDER_TRANSFORM}

{$IFNDEF SRL_OSR}
  {$I SRL/OSR.simba}
{$ENDIF}

{$include_once ../Utils/Utils.simba}

{$include_once Types.simba}
{$include_once Utils.simba}

{
  Transforms the item with a tool
}
procedure TBot.Transform(Item: TRSItem; MaterialSlots, ToolSlots: TIntegerArray);
var
  ToolSlot, MaterialSlot, MCount, MC, StopShort, OGMCount: Int32;
  Made: Boolean;
  CountDown: TCountDown;
begin
  // can be secondary material too!
  ToolSlot := ToolSlots[Random(High(ToolSlots))];

  MaterialSlot := MaterialSlots[Random(High(MaterialSlots))];
  MCount := Length(MaterialSlots);
  OGMCount := MCount;

  StopShort := MCount;
  if Random() < 0.05 then
    StopShort := SRL.SkewedRand(MCount, MCount, 0);

  Script.WriteMsg('Starting the transformation', True);

  // start transforming
  if Random() < 0.5 then
  begin
    Inventory.SetSelectedSlot(ToolSlot);
    Wait(0, 1000, wdLeft);
    Inventory.ClickSlot(MaterialSlot);
  end
  else
  begin
    Inventory.SetSelectedSlot(MaterialSlot);
    Wait(0, 1000, wdLeft);
    Inventory.ClickSlot(ToolSlot);
  end;

  if Self.MakeNum = -1 then
    Made := True;

  if not Made and Make.IsOpen(SRL.SkewedRand(1500, 1250, 2500)) then
  begin
    Wait(0, 3333, wdLeft);
    if Length(Make.GetItemButtons()) > Self.MakeNum then
      Made := Make.Select(Self.MakeNum, MAKE_QUANTITY_ALL, Script.ShouldPressKeyboard());
  end;

  if not Made then
  begin
    Script.WriteMsg('Failed to open make', True);
    exit();
  end;

  Self.AfterActionAntiBan(True, False);
  CountDown.Init(Self.TransformWaitTime);

  Script.WriteMsg('Making...', True);

  while RSClient.IsLoggedIn() do
  begin
    if Self.HandleAmuletOfChem() then
    begin
      Script.WriteMsg('Transform: HandleAmuletOfChem!', True);
      break;
    end;

    // check inv
    MaterialSlots.Clear();
    Inventory.FindItem(Item, MaterialSlots);
    MC := Length(MaterialSlots);

    if MC < Self.MaterialRatio then
    begin
      Script.WriteMsg('Transform: No more material', True);
      break;
    end;

    if MC <> MCount then
    begin
      Script.WriteMsg('Transform: InvCount changed, restarting countdown', True);
      CountDown.Restart();
    end;

    if (StopShort - (OGMCount - MC)) <= 0 then
    begin
      if Random() < 0.65 then
        Self.StoppedShort := True;

      Script.WriteMsg('Stopping short for antiban', True);
      break;
    end;

    MCount := MC;

    if CountDown.IsFinished() then
    begin
      Script.WriteMsg('Transform: Countdown expired', True);
      break;
    end;

    if Chat.LeveledUp() then
    begin
      Script.WriteMsg('Transform: Leveld', True);
      if Random() < 0.2 then
      begin
        Wait(0, 1500, wdLeft);

        while Chat.ClickContinue(Script.ShouldPressKeyboard()) do
          Wait(75, 500);
      end;
      break;
    end;

    if not Self.ShouldMakeProduct() then
    begin
      Script.WriteMsg('Transform: shouldn''t make product', True);
      break;
    end;

    Self.DoMainLoopStuff('Transforming items...', True);
  end;

  Wait(0, 5555, wdLeft);
end;

{
  Does transform type
}
function TBot.DoTransform(): Boolean;
var
  MaterialSlots, ToolSlots: TIntegerArray;
  Item: TRSItem;
begin
  if not Inventory.FindItems(Self.ToolItems, ToolSlots) then
  begin
    Script.WriteMsg('No tools', True);
    exit();
  end;

  Script.WriteMsg('DoTransform: ToolSlots: ' + ToStr(ToolSlots), True);

  Script.WriteMsg('Transforming items...', True);

  for Item in Self.MaterialItems do
  begin
    if Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('DoTransform: MaterialSlots: ' + ToStr(MaterialSlots), True);
      Self.Transform(Item, MaterialSlots, ToolSlots);
      Result := True;
      break;
    end;
  end;

  Script.WriteMsg('No materials', True);
end;

{
  Combines
}
function TBot.DoCombine(): Boolean;
var
  MaterialSlots, SecondaryMaterialSlots: TIntegerArray;
  Item: TRSItem;
begin
  if not Inventory.FindItems(Self.SecondaryMaterialItems, SecondaryMaterialSlots) then
  begin
    Script.WriteMsg('No secondary materials', True);
    exit();
  end;

  Script.WriteMsg('DoCombine: SecondaryMaterialSlots: ' + ToStr(SecondaryMaterialSlots), True);

  Script.WriteMsg('Combining items...', True);

  for Item in Self.MaterialItems do
  begin
    if Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('DoCombine: MaterialSlots: ' + ToStr(MaterialSlots), True);
      Self.Transform(Item, MaterialSlots, SecondaryMaterialSlots);
      Result := True;
      break;
    end;
  end;

  Script.WriteMsg('No materials', True);
end;

{
  Cleans the herbs
}
function TBot.Clean(Item: TRSItem; out Pattern: TIntegerArray; i: Int32; out AfkClean: Boolean): Boolean;
var
  MaterialSlots: TIntegerArray;
  MCount, OGMCount, StopShort, ii, MC: Int32;
  CountDown: TCountDown;
  Uptxt: String;
begin
  i := i;
  if not Inventory.FindItem(Item, MaterialSlots) then
    exit();

  MCount := Length(MaterialSlots);
  OGMCount := MCount;

  StopShort := MCount;
  if Random() < 0.05 then
    StopShort := SRL.SkewedRand(MCount, MCount, 0);

  // remove all slots with no item!
  for ii := High(Pattern) downto Low(Pattern) do
    if MaterialSlots.Find(Pattern[ii]) = -1 then
      Pattern.Remove(Pattern[ii]);

  if Length(Pattern) <= 0 then
    exit();

  Mouse.Move(Inventory.GetSlotBox(Pattern[0]));

  Uptxt := MainScreen.GetUpText();
  if (Uptxt.Count('Clean') > 0) and (Random() < 0.975) then
  begin
    Mouse.Click(MOUSE_LEFT);
    Result := True;
  end
  else
    Result := ChooseOption.Select('Clean');

  Pattern.Remove(Pattern[0]);

  if Result and AfkClean then
  begin
    Script.WriteMsg('Starting the cleaning', True);

    CountDown.Init(Self.TransformWaitTime);

    while RSClient.IsLoggedIn() do
    begin
      // check inv
      MaterialSlots.Clear();
      Inventory.FindItem(Item, MaterialSlots);
      MC := Length(MaterialSlots);

      if MC < Self.MaterialRatio then
      begin
        Script.WriteMsg('Clean: No more material', True);
        break;
      end;

      if MC <> MCount then
      begin
        Script.WriteMsg('Clean: InvCount changed, restarting countdown', True);
        CountDown.Restart();
      end;

      if (StopShort - (OGMCount - MC)) <= 0 then
      begin
        if Random() < 0.65 then
          Self.StoppedShort := True;

        Script.WriteMsg('Stopping short for antiban', True);
        break;
      end;

      MCount := MC;

      if CountDown.IsFinished() then
      begin
        Script.WriteMsg('Clean: Countdown expired', True);
        break;
      end;

      if Chat.LeveledUp() then
      begin
        Script.WriteMsg('Clean: Leveld', True);
        if Random() < 0.2 then
        begin
          Wait(0, 1500, wdLeft);

          while Chat.ClickContinue(Script.ShouldPressKeyboard()) do
            Wait(75, 500);
        end;
        break;
      end;

      if Random() < (0.05 / 14) then
      begin
        AfkClean := False;
        break;
      end;

      if not Self.ShouldMakeProduct() then
      begin
        Script.WriteMsg('Clean: shouldn''t make product', True);
        break;
      end;

      Self.DoMainLoopStuff('Cleaning items...', True);
    end;
  end
  else if Random() < (0.05 / 28) then
    Self.StoppedShort := True;
end;


{
  Clean the herbs
}
function TBot.DoClean(): Boolean;
var
  Item: TRSItem;
  Pattern, MaterialSlots: TIntegerArray;
  i, S: Int32;
  AfkClean: Boolean;
begin
  Pattern := Bank.GetGoodPattern();

  S := Mouse.Speed;
  Mouse.Speed += Script.FastMouseAdd + Random(-2, 2);

  if Random() < 0.05 then
    AfkClean := True;

  while RSClient.IsLoggedIn() do
  begin
    // make sure nothing is selected
    Inventory.SetSelectedSlot(-1);

    MaterialSlots.Clear();
    if not Inventory.FindItems(Self.MaterialItems, MaterialSlots) or (Length(MaterialSlots) < Self.MaterialRatio) then
    begin
      Script.WriteMsg('DoClean: no mats', True);
      break;
    end;

    if Self.StoppedShort then
    begin
      Script.WriteMsg('DoClean: stopping short', True);
      break;
    end;

    if not Self.ShouldMakeProduct() then
    begin
      Script.WriteMsg('DoClean: shouldn''t make product', True);
      break;
    end;

    if Random() < (0.05 / 28) then
      AfkClean := True;

    for Item in Self.MaterialItems do
    begin
      if Self.Clean(Item, Pattern, i, AfkClean) then
        Result := True;
    end;

    if Length(Pattern) < Self.MaterialRatio then
    begin
      Self.StoppedShort := True;
      break;
    end;

    Inc(i);
  end;

  Mouse.Speed := S;
  Wait(0, 5555, wdLeft);
  Script.WriteMsg('No materials', True);
end;

{
  Does the enchanting
}
function TBot.Enchant(Item: TRSItem; Pattern: TIntegerArray): Boolean;
var
  B: TBox;
  MCount, OGMCount, StopShort, i, MC: Int32;
  ItemSlots: TIntegerArray;
  CountDown: TCountDown;    
  AfkEnchant: Boolean;
begin
  if Random() < 0.05 then
    AfkEnchant := True;

  while RSClient.IsLoggedIn() do
  begin
    // open magic book
    if not Magic.Open() then
    begin
      Script.WriteMsg('Failed to open magic', True);
      break;
    end;

    if not Magic.GetSpellBox(Self.Spell, B) then
    begin
      Script.WriteMsg('Failed to GetSpellBox', True);
      break;
    end;

    // start the casts!
    Script.WriteMsg('Casting spell', True);
    Wait(0, 1250, wdLeft);
    Mouse.Click(B, MOUSE_LEFT);

    // make sure the inv opens
    CountDown.Init(SRL.SkewedRand(2000, 1500, 2500));
    while RSClient.IsLoggedIn() do
    begin
      if Inventory.IsOpen() then
        break;

      if CountDown.IsFinished() then
        break;

      Self.DoMainLoopStuff('Waiting for inv to open', False);
    end;

    if not Inventory.IsOpen() then
    begin
      Script.WriteMsg('Inv didnt open', True);
      break;
    end;

    Script.WriteMsg('Inv opened', True); 
    Wait(0, 1250, wdLeft);

    if not Self.ShouldMakeProduct() then
      break;

    // make sure theres mats
    if not Inventory.FindItems(Self.ToolItems) then
    begin
      Script.WriteMsg('No runes found!', True);
      break;
    end;

    ItemSlots.Clear();
    if not Inventory.FindItem(Item, ItemSlots) or (Length(ItemSlots) < Self.MaterialRatio) then
    begin
      Script.WriteMsg('No materials found!', True);
      break;
    end;

    // update pattern
    for i := High(Pattern) downto Low(Pattern) do
      if ItemSlots.Find(Pattern[i]) = -1 then
        Pattern.Remove(Pattern[i]);

    if Length(Pattern) <= 0 then
      break;

    i := Pattern[0];
    Pattern.Remove(i);

    // start enchanting

    // make sure that we are casting
    Mouse.Move(Inventory.GetSlotBox(i));
    if not MainScreen.IsUpText('->') then
    begin
      Script.WriteMsg('Not casting!', True);
      break;
    end;

    Script.WriteMsg('Enchanting item', True);
    Mouse.Click(Inventory.GetSlotBox(i), MOUSE_LEFT);

    // move our mouse to the spot again
    if Random() < 0.8 then
    begin
      Wait(0, 1250, wdLeft);
      Mouse.Move(B.Expand(10));
    end;

    // wait for enchant, we should swap back to the magic tab when it begins
    CountDown.Init(SRL.SkewedRand(4000, 3500, 6000));
    while RSClient.IsLoggedIn() do
    begin
      if Magic.IsOpen() then
        break;

      if CountDown.IsFinished() then
        break;

      Self.DoMainLoopStuff('Waiting for magic to open', False);
    end;

    if not Magic.IsOpen() then
    begin
      Script.WriteMsg('Magic didnt reopen', True);
      break;
    end;

    Result := True;
    Script.WriteMsg('Magic opened', True);

    MCount := Length(Pattern);
    OGMCount := MCount;

    StopShort := MCount;
    if Random() < 0.05 then
      StopShort := SRL.SkewedRand(MCount, MCount, 0);

    if Random() < (0.05 / 28) then
      AfkEnchant := True;

    if AfkEnchant then
    begin
      // do afk enchanting
      CountDown.Init(Self.TransformWaitTime);
      while RSClient.IsLoggedIn() do
      begin
        // check inv
        ItemSlots.Clear();
        Inventory.FindItem(Item, ItemSlots);
        MC := Length(ItemSlots);

        if MC < Self.MaterialRatio then
        begin
          Script.WriteMsg('Enchant: No more material', True);
          break;
        end;

        if MC <> MCount then
        begin
          Script.WriteMsg('Enchant: InvCount changed, restarting countdown', True);
          CountDown.Restart();
        end;

        if (StopShort - (OGMCount - MC)) <= 0 then
        begin
          if Random() < 0.65 then
            Self.StoppedShort := True;

          Script.WriteMsg('Stopping short for antiban', True);
          break;
        end;

        MCount := MC;

        if CountDown.IsFinished() then
        begin
          Script.WriteMsg('Enchant: Countdown expired', True);
          break;
        end;

        if Chat.LeveledUp() then
        begin
          Script.WriteMsg('Enchant: Leveld', True);
          if Random() < 0.2 then
          begin
            Wait(0, 1500, wdLeft);

            while Chat.ClickContinue(Script.ShouldPressKeyboard()) do
              Wait(75, 500);
          end;
          break;
        end;

        if Random() < (0.05 / 14) then
        begin
          AfkEnchant := False;
          break;
        end;

        if not Self.ShouldMakeProduct() then
        begin
          Script.WriteMsg('Enchant: shouldn''t make product', True);
          break;
        end;

        Self.DoMainLoopStuff('Enchanting items...', True);
      end;
    end
    else
    begin
      if Random() < (0.05 / 28) then
      begin
        Self.StoppedShort := True;
        break;
      end;

      Wait(0, 3000, wdLeft);
    end;

    if Self.StoppedShort then
      break;

    if Length(Pattern) <= 0 then
      break;
  end;
end;

{
  Enchants!
}
function TBot.DoEnchant(): Boolean;
var
  MaterialSlots, Pattern: TIntegerArray;
  Item: TRSItem;
  i: Int32;
begin
  // runes are tools!
  if not Inventory.FindItems(Self.ToolItems) then
  begin
    Script.WriteMsg('No runes', True);
    exit();
  end;

  Script.WriteMsg('Enchanting items...', True);
  Pattern := Bank.GetGoodPattern();
  Script.WriteMsg('DoEnchant: Pattern: ' + ToStr(Pattern), True);

  for Item in Self.MaterialItems do
  begin
    if Inventory.FindItem(Item, MaterialSlots) then
    begin
      Script.WriteMsg('DoEnchant: MaterialSlots: ' + ToStr(MaterialSlots), True);

      // only include items in pattern
      for i := High(Pattern) downto Low(Pattern) do
        if MaterialSlots.Find(Pattern[i]) = -1 then
          Pattern.Remove(Pattern[i]);

      Result := Self.Enchant(Item, Pattern);
      break;
    end;
  end;

  Script.WriteMsg('No materials', True);
end;

{$ENDIF} // INEEDBOT_BANKSTANDER_TRANSFORM
