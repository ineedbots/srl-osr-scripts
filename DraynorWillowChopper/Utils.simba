{
  Draynor Village Chopper utils
  Author: ineedbot
  Date: 7/4/2022

  Utils
}

{$IFNDEF INEEDBOT_DVC_UTILS}
{$DEFINE INEEDBOT_DVC_UTILS}

{$IFNDEF SRL_OSR}
  {$I SRL/OSR.simba}
{$ENDIF}

{$include_once ../Utils/Utils.simba}

{$include_once Types.simba}
{$include_once Config.simba}

{$include_once Bank.simba}
{$include_once Tree.simba}
{$include_once Misc.simba}

{
  Updates which fish tile we are at
}
procedure TBot.UpdateWalkedTree(Here: Boolean = True);
var
  CTreeTiles: TPointArray;
begin
  if Here then
  begin
    CTreeTiles := Copy(Self.TreeTiles);
    CTreeTiles.Sort(Self.RSW.GetMyPos());

    Self.LastWalkedTree := Self.WalkedTree;
    Self.WalkedTree := Self.TreeTiles.Find(CTreeTiles[0]);
    if Self.LastWalkedTree <> Self.WalkedTree then
      Self.WalkedTreeForward := Self.LastWalkedTree < Self.WalkedTree;
  end
  else
  begin
    if Length(Self.TreeTiles) > 1 then
    begin
      if Self.WalkedTreeForward then
      begin
        Self.WalkedTree += 1;
        if Self.WalkedTree > High(Self.TreeTiles) then
        begin
          Self.WalkedTree -= 2;
          Self.WalkedTreeForward := False;
        end;
      end
      else
      begin
        Self.WalkedTree -= 1;
        if Self.WalkedTree < Low(Self.TreeTiles) then
        begin
          Self.WalkedTree += 2;
          Self.WalkedTreeForward := True;
        end;
      end;
    end;
  end;
end;

{
  From Flight's AIOFisher
}
procedure TBot.DoSkillHover();
begin
  case Random() of
    0.0..0.6: Script.Antiban.HoverSkill(ERSSKILL.WOODCUTTING, Random(1700, 3200), True);
    0.6..0.8: Script.Antiban.HoverSkill(ERSSKILL.FLETCHING, Random(1700, 3200), True);
    else Script.Antiban.HoverSkill(ERSSKILL.FIREMAKING, Random(1700, 3200), True);
  end;
end;

{
  Enables special attack
}
function TBot.CheckSpec(): Boolean;
begin
  if not Self.Specing then
    exit();

  if Minimap.GetOrbLevel(Minimap.GetSpecAttackCircle()) >= 100 then
  begin
    Script.WriteMsg('Special attack boosting');

    Mouse.Click(Minimap.GetSpecAttackCircle(), MOUSE_LEFT);

    Wait(1000, 2000, wdLeft);
    Result := True;
  end;
end;

{
  Checks if we are under attack
}
function TBot.CheckUnderAttack(): Boolean;
begin
  if Script.CheckUnderAttack() then
  begin
    Result := True;

    Self.GoToBank(True);

    if Script.HPAmount < 5 then
      Script.Stop('Critical HP!')
    else
      Wait(5000, 15000);
  end;
end;

{
  Does general checks and does antiban.
}
function TBot.DoAntiBan(DoInterfaceClose: Boolean = False): Boolean;
var
  Uptxt: String;
begin
  Script.DoGeneralChecks();

  if not RSClient.IsLoggedIn() then
    exit();

  // are we under attack?
  Self.CheckUnderAttack();

  // check we we leveled up
  if Chat.LeveledUp() then
  begin
    Wait(0, 2500, wdLeft);
    while Chat.ClickContinue(Script.ShouldPressKeyboard()) do
      Wait(75, 500);
  end;

  Self.CheckSpec();

  if DoInterfaceClose and MainScreen.AnyInterfaceOpen() then
    MainScreen.CloseInterface();

  // make sure nothing is selected in our inventory, might prevent us from interacting with stuff
  Inventory.SetSelectedSlot(-1);

  Uptxt := MainScreen.GetUpText();
  if Uptxt.Count('->') > 0 then
    Mouse.Click(MOUSE_LEFT);

  // check for birdnest
  Self.CheckBirdnest();

  // general antiban
  Result := Script.Antiban.DismissRandom() or Script.Antiban.DoAntiban(Script.AllowBreaks, Script.AllowBreaks);
end;

{
  From Slacky's BarbFisher
}
procedure TBot.AfterActionAntiBan(DoAB: Boolean = True; DoInterfaceClose: Boolean = True; WasFailure: Boolean = False);
begin
  Wait(0, 750, wdLeft);

  if DoAB then
    Self.DoAntiban(DoInterfaceClose);

  if WasFailure then
    MainScreen.SetHighestPitch();
end;

{
  Debug draws the stuff
}
procedure TBot.DebugDraw();
var
  ATPA: T2DPointArray;
  TPA: TPointArray;
  TreeTile, OurTile, BankTile, FireTile, TP: TPoint;
  TileRect: TRectangle;
  i: Int32;
  CompAng: Double;
  Text: String;
  B: TBox;
  BA: TBoxArray;
  T, L, K: TIntegerArray;
begin
  OurTile := Self.RSW.GetMyPos();
  CompAng := Minimap.GetCompassAngle(False);

  Text := 'Pos: ' + ToStr(OurTile);
  RSClient.Image.DrawText(Text, [MainScreen.X1, MainScreen.Y1 + 20], clGreen);

  if not MainScreen.AnyInterfaceOpen() then
  begin
    for TreeTile in Self.TreeTiles do
    begin
      TileRect := Self.FindTreeTile(OurTile, TreeTile);
      B := Self.GetTreeSearchBox(TileRect); 
      ATPA := Self.FindTreeColor(B);

      RSClient.Image.DrawBox(B, clGreen);
      RSClient.Image.DrawATPA(ATPA);
      RSClient.Image.DrawRect(TileRect, clRed);
      RSClient.Image.DrawCross(Self.RSW.WorldToMM(OurTile, TreeTile, CompAng), 4, clBlue);

      if Self.FindTree(OurTile, TreeTile, B) then
        RSClient.Image.DrawBox(B, clBlue);
    end;

    for BankTile in Self.BankTiles do
    begin
      TileRect := Self.FindBankTile(OurTile, BankTile);
      B := Self.GetBankSearchBox(TileRect);
      ATPA := Self.FindBankColor(B);

      RSClient.Image.DrawBox(B, clGreen);
      RSClient.Image.DrawATPA(ATPA);
      RSClient.Image.DrawRect(TileRect, clRed);
      RSClient.Image.DrawCross(Self.RSW.WorldToMM(OurTile, BankTile, CompAng), 4, clBlue);

      if Self.FindBank(OurTile, BankTile, B) then
        RSClient.Image.DrawBox(B, clBlue);
    end;

    for i := Low(Self.FireStartTiles) to High(Self.FireStartTiles) do
    begin
      FireTile := Self.FireStartTiles[i].Tile;

      TileRect := Self.FindFireTile(OurTile, FireTile);
      B := Self.GetFireSearchBox(TileRect);  
      ATPA := Self.FindFireColor(B);

      RSClient.Image.DrawBox(B, clGreen);
      RSClient.Image.DrawATPA(ATPA);
      RSClient.Image.DrawRect(TileRect, clRed);
      RSClient.Image.DrawCross(Self.RSW.WorldToMM(OurTile, FireTile, CompAng), 4, clBlue);

      if Self.FindFire(OurTile, FireTile, B) then
        RSClient.Image.DrawBox(B, clBlue);
    end;

    TPA := Self.GetNestItemDots();
    for TP in TPA do
    begin
      TileRect := Self.FindNestTile(TP, CompAng);
      B := Self.GetNestSearchBox(TileRect);
      ATPA := Self.FindBirdNestColor(B);

      RSClient.Image.DrawBox(B, clOrange);
      RSClient.Image.DrawATPA(ATPA);
      RSClient.Image.DrawRect(TileRect, clPurple);
    end;

    if Self.FoundBirdsNest(BA) then
    begin
      for B in BA do
        RSClient.Image.DrawBox(B, clBlack);
    end;
  end;

  if Inventory.IsOpen() then
  begin
    Inventory.FindItems(Self.LogItems, L);
    Inventory.FindItems(Self.ToolItems, T);
    Inventory.FindItems(Self.KnifedLogItems, K);

    for i := 0 to 27 do
    begin
      if L.Find(i) <> -1 then
        RSClient.Image.DrawBox(Inventory.GetSlotBox(i), clWhite)
      else if T.Find(i) <> -1 then
        RSClient.Image.DrawBox(Inventory.GetSlotBox(i), clRed)
      else if K.Find(i) <> -1 then
        RSClient.Image.DrawBox(Inventory.GetSlotBox(i), clPurple);
    end;
  end;
end;

{
  Draws the stuff onto the screen
}
procedure TBot.Draw();
begin
  if not Script.CanDraw() then
    exit();

  RSClient.Image.Clear();
  // TODO complete

  if Script.Debug then
    Self.DebugDraw();
end;

{
  Does the proggy report to the console window
}
procedure TBot.ReportConsole();
var
  WriteOut: String;
begin
  if not Script.CanReportConsole() then
    exit();

  WriteOut += 'Banking: ' + ToStr(Self.Banking) + LineEnding;
  WriteOut += 'Firemaking: ' + ToStr(Inventory.FindItem(Self.TinderItem)) + LineEnding;
  WriteOut += 'Fletching: ' + ToStr(Inventory.FindItem(Self.KnifeItem)) + LineEnding;
  WriteOut += 'Runtime: ' + SRL.MsToTime(Script.TrueRunTime.ElapsedTime(), Time_Short) + LineEnding;
  WriteOut += 'Status: ' + Script.Status + LineEnding;
  WriteOut += 'Energy: ' + ToStr(Script.AntiBanEnergy.EnergyLevel(Script.Antiban)) + LineEnding;
  WriteOut += 'Chopped: ' + IntToStr(Self.LogsDone) + LineEnding;
  WriteOut += 'Taking long breaks: ' + ToStr(TAKEBREAKS) + LineEnding;
  WriteOut += 'Time until break: ' + Script.Antiban.TimeUntilBreak(Script.Antiban.Breaks[0]) + LineEnding;
  WriteOut += 'Time until shutdown: ' + SRL.MsToTime(Script.ShutdownTime - GetTimeRunning(), Time_Short) + LineEnding;

  ClearDebug();
  Writeln(WriteOut);
end;

{
  General main loop stuff to do.
}
procedure TBot.DoMainLoopStuff(Status: String = ''; DoAB: Boolean = True);
begin
  if Status <> '' then
    Script.SetStatus(Status);

  if DoAB then
    Self.DoAntiBan();

  Self.ReportConsole();
  Self.Draw();

  // check if we gained xp, failsafe
  if RSClient.IsLoggedIn() then
    Script.CheckActivity();

  Wait(Script.MainLoopWaitAmount(True));
end;

{$ENDIF} // INEEDBOT_DVC_UTILS
