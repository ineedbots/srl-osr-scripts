{
  Edgeville smelter utils
  Author: ineedbot
  Date: 7/9/2022

  Utils
}

{$IFNDEF INEEDBOT_EVS_UTILS}
{$DEFINE INEEDBOT_EVS_UTILS}

{$IFNDEF SRL_OSR}
  {$I SRL/OSR.simba}
{$ENDIF}

{$include_once ../Utils/Utils.simba}

{$include_once Types.simba}
{$include_once Config.simba}

{
  Handles the doors while walking
}
procedure WalkHandleDoor(Walker: PRSWalker; Position: TPoint; Destination: TPoint);
begin
  Walker := Walker;
  Position := Position;
  Destination := Destination;
end;

{$include_once Bank.simba}
{$include_once Station.simba}
{$include_once Door.simba}

{
  Handles ring of forging
}
function TBot.HandleRingOfForging(): Boolean;
var
  ChatMsg: String;
begin
  if not Self.UsingRingOfForging or MainScreen.AnyInterfaceOpen() then
    exit();

  if Self.RingOfForgingCountdown.IsFinished() then
  begin
    Self.NeedToRefillRingOfForging := True;
    exit(True);
  end;

  if not Chat.IsOpen() then
    exit();

  ChatMsg := Chat.GetMessage(7, [$7C0C7E]).Strip();

  if not ChatMsg.IsInString(Self.RingOfForgingBrokeTexts) then
    exit();

  Script.ExamineRandomItem();

  Self.NeedToRefillRingOfForging := True;
  exit(True);
end;

{
  From Flight's AIOFisher
}
procedure TBot.DoSkillHover();
begin
  case Random() of
    0.0..0.6: Script.Antiban.HoverSkill(ERSSKILL.CRAFTING, Random(1700, 3200), True);
    else Script.Antiban.HoverSkill(ERSSKILL.SMITHING, Random(1700, 3200), True);
  end;
end;

{
  Does general checks and does antiban.
}
function TBot.DoAntiBan(DoInterfaceClose: Boolean = False): Boolean;
begin
  Script.DoGeneralChecks();

  if not RSClient.IsLoggedIn() then
    exit();

  // check we we leveled up
  if Chat.LeveledUp() then
  begin
    Wait(0, 2500, wdLeft);
    while Chat.ClickContinue() do
      Wait(75, 500);
  end;

  if DoInterfaceClose and MainScreen.AnyInterfaceOpen() then
    MainScreen.CloseInterface();

  // make sure nothing is selected in our inventory, might prevent us from interacting with stuff
  Inventory.SetSelectedSlot(-1);

  if MainScreen.IsUpText('->') then
    Mouse.Click(MOUSE_LEFT);

  // general antiban
  Result := Script.Antiban.DismissRandom() or Script.Antiban.DoAntiban(Script.AllowBreaks, Script.AllowBreaks);
end;

{
  From Slacky's BarbFisher
}
procedure TBot.AfterActionAntiBan(DoAB: Boolean = True; DoInterfaceClose: Boolean = True);
begin
  Wait(0, 750, wdLeft);

  if DoAB then
    Self.DoAntiban(DoInterfaceClose);
end;

{
  Debug draws the stuff
}
procedure TBot.DebugDraw();
var
  ATPA: T2DPointArray;
  OurTile, TP: TPoint;
  TPA, TPA2: TPointArray;
  TileRect: TRectangle;
  i: Int32;
  CompAng: Double;
  Text: String;
  B: TBox;
  T, M, P: TIntegerArray;
  Item: TRSItem;     
  BtnArr: TRSButtonArray;
  Door: TDoor;
begin
  OurTile := Self.RSW.GetMyPos();
  CompAng := Minimap.GetCompassAngle(False);

  Text := 'Pos: ' + ToStr(OurTile);
  RSClient.Image.DrawText(Text, [MainScreen.X1, MainScreen.Y1 + 20], clGreen);

  if not MainScreen.AnyInterfaceOpen() then
  begin
    for TP in Self.BankTiles do
    begin
      TileRect := Self.FindBankTile(OurTile, TP);
      B := Self.GetBankSearchBox(TileRect);
      ATPA := Self.FindBankColor(B);

      RSClient.Image.DrawBox(B, clGreen);
      RSClient.Image.DrawRect(TileRect, clRed);
      RSClient.Image.DrawATPA(ATPA);
      RSClient.Image.DrawCross(Self.RSW.WorldToMM(OurTile, TP, CompAng), 4, clWhite);

      if Self.FindBank(OurTile, TP, B) then
        RSClient.Image.DrawBox(B, clBlue);
    end;

    for TP in Self.StationTiles do
    begin
      TileRect := Self.FindStationTile(OurTile, TP);
      B := Self.GetStationSearchBox(TileRect);
      ATPA := Self.FindStationColor(B);

      RSClient.Image.DrawBox(B, clGreen);
      RSClient.Image.DrawRect(TileRect, clRed);
      RSClient.Image.DrawATPA(ATPA);
      RSClient.Image.DrawCross(Self.RSW.WorldToMM(OurTile, TP, CompAng), 4, clWhite);

      if Self.FindStation(OurTile, TP, B) then
        RSClient.Image.DrawBox(B, clBlue);
    end;

    for Door in Self.Doors do
    begin
      TileRect := Self.FindDoorTile(OurTile, Door.ClosedTile);
      B := Self.GetDoorSearchBox(TileRect, Door.ClosedColor);
      ATPA := Self.FindDoorColor(B, Door.ClosedColor);

      RSClient.Image.DrawBox(B, clGreen);
      RSClient.Image.DrawRect(TileRect, clRed);
      RSClient.Image.DrawATPA(ATPA);
      RSClient.Image.DrawCross(Self.RSW.WorldToMM(OurTile, Door.ClosedTile, CompAng), 4, clWhite);

      if Self.FindDoor(OurTile, Door.ClosedTile, Door.ClosedColor, B) then
        RSClient.Image.DrawBox(B, clBlue);

      for TPA in Door.Zones do
      begin
        TPA2 := Copy(TPA);
        for i := Low(TPA2) to High(TPA2) do
          TPA2[i] := Self.RSW.WorldToMM(OurTile, TPA2[i], CompAng);

        RSClient.Image.DrawPoly(TPA2, clRed);
      end;

      if Door.HasOpenDoorData then
      begin
        TileRect := Self.FindDoorTile(OurTile, Door.OpenedTile);
        B := Self.GetDoorSearchBox(TileRect, Door.OpenedColor);
        ATPA := Self.FindDoorColor(B, Door.OpenedColor);

        RSClient.Image.DrawBox(B, clRed);
        RSClient.Image.DrawRect(TileRect, clGreen);
        RSClient.Image.DrawATPA(ATPA);
        RSClient.Image.DrawCross(Self.RSW.WorldToMM(OurTile, Door.OpenedTile, CompAng), 4, clBlack);

        if Self.FindDoor(OurTile, Door.OpenedTile, Door.OpenedColor, B) then
          RSClient.Image.DrawBox(B, clOrange);
      end;
    end;

    if Make.IsOpen() then
    begin
      BtnArr := Make.GetItemButtons();

      if (Self.MakeOption >= 0) and (Length(BtnArr) > Self.MakeOption) then
      begin
        B := BtnArr[Self.MakeOption].Bounds;
        RSClient.Image.DrawBox(B, clBlue);
      end;
    end;
  end
  else
  begin
    if Bank.IsOpen() then
    begin
      for Item in Self.MaterialItems do
      begin
        if Bank.FindItem(Item, B) then
          RSClient.Image.DrawBox(B, clPurple);
      end;
    end;
  end;

  if Inventory.IsOpen() then
  begin
    Inventory.FindItems(Self.ProductItems, P);
    Inventory.FindItems(Self.ToolItems, T);
    Inventory.FindItems(Self.MaterialItems, M);

    for i := 0 to 27 do
    begin
      if P.Find(i) <> -1 then
        RSClient.Image.DrawBox(Inventory.GetSlotBox(i), clWhite)
      else if T.Find(i) <> -1 then
        RSClient.Image.DrawBox(Inventory.GetSlotBox(i), clRed)
      else if M.Find(i) <> -1 then
        RSClient.Image.DrawBox(Inventory.GetSlotBox(i), clPurple);
    end;
  end;
end;

{
  Draws the stuff onto the screen
}
procedure TBot.Draw();
begin
  if not Script.CanDraw() then
    exit();

  RSClient.Image.Clear();
  // TODO complete

  if Script.Debug then
    Self.DebugDraw();
end;

{
  Does the proggy report to the console window
}
procedure TBot.ReportConsole();
var
  WriteOut: String;
begin
  if not Script.CanReportConsole() then
    exit();

  WriteOut += 'Runtime: ' + SRL.MsToTime(Script.TrueRunTime.ElapsedTime(), Time_Short) + LineEnding;
  WriteOut += 'Status: ' + Script.Status + LineEnding;
  WriteOut += 'Energy: ' + ToStr(Script.AntiBanEnergy.EnergyLevel(Script.Antiban)) + LineEnding;
  WriteOut += 'Did: ' + IntToStr(Self.Did) + LineEnding;
  WriteOut += 'Taking long breaks: ' + ToStr(TAKEBREAKS) + LineEnding;
  WriteOut += 'Time until break: ' + Script.Antiban.TimeUntilBreak(Script.Antiban.Breaks[0]) + LineEnding;
  WriteOut += 'Time until shutdown: ' + SRL.MsToTime(Script.ShutdownTime - GetTimeRunning(), Time_Short) + LineEnding;

  ClearDebug();
  Writeln(WriteOut);
end;

{
  General main loop stuff to do.
}
procedure TBot.DoMainLoopStuff(Status: String = ''; DoAB: Boolean = True);
begin
  if Status <> '' then
    Script.SetStatus(Status);

  if DoAB then
    Self.DoAntiBan();

  Self.ReportConsole();
  Self.Draw();

  // check if we gained xp, failsafe
  if RSClient.IsLoggedIn() then
    Script.CheckActivity();

  Wait(Script.MainLoopWaitAmount(True));
end;

{
  Handles the doors while walking
}
procedure WalkHandleDoor(Walker: PRSWalker; Position: TPoint; Destination: TPoint); override;
var
  Clicked: Boolean;
begin
  while RSClient.IsLoggedIn() and Bot.HandleDoors(Position, Destination, Clicked) do
  begin
    Position := Walker^.GetMyPos();

    if not Clicked then
      break;
  end;
end;

{$ENDIF} // INEEDBOT_EVS_UTILS
