{
  Motherload Miner sack
  Author: ineedbot
  Date: 8/27/2022

  Sack
}

{$IFNDEF INEEDBOT_MLM_SACK}
{$DEFINE INEEDBOT_MLM_SACK}

{$IFNDEF SRL_OSR}
  {$I SRL/OSR.simba}
{$ENDIF}

{$include_once ../Utils/Utils.simba}

{$include_once Types.simba}
{$include_once Utils.simba}

{
  Finds the sack tile
}
function TBot.FindSackTile(OurPos: TPoint; Height: Int32): TRectangle;
begin
  Result := Self.RSW.GetTileMSEx(OurPos, Self.SackTile, Self.SackHeight - Height);
end;

{
  Gets the search box
}
function TBot.GetSackSearchBox(SackTile: TRectangle): TBox;
begin
  Result := SackTile.Expand(Round(SackTile.Radius() * Self.SackColor.RadiusExpand)).Bounds();
end;

{
  Returns all object colors within the box
}
function TBot.FindSackColor(Area: TBox): T2DPointArray;
begin
  Result := MainScreen.FindObject(Self.SackColor.RSObject, Area);
  Result.FilterSize(MainScreen.ConvertDistance(Self.SackColor.FilterSize), __GT__);
end;

{
  Returns true if the sack is visible, B is the bounds of the sack on the screen
}
function TBot.FindSack(OurTile: TPoint; Height: Int32; out B: TBox): Boolean;
var
  TileRect: TRectangle;
  ATPA: T2DPointArray;
begin
  TileRect := Self.FindSackTile(OurTile, Height);

  B := Self.GetSackSearchBox(TileRect);

  if not MainScreen.IsVisible(B.Middle()) then
    exit();

  Result := True;

  B.LimitTo(MainScreen.Bounds());

  ATPA := Self.FindSackColor(B);

  if Length(ATPA) <= 0 then
    B := TileRect.Bounds().Expand(-MainScreen.ConvertDistance(Self.SackColor.NoColorTileExpand)) // couldnt find color... use the fixd tile
  else
    B := ATPA.Biggest().Bounds();
end;

{
  Returns true if found the sack, WasClicked true if clicked it
}
function TBot.ClickSack(out WasClicked: Boolean; OurTile: TPoint = [-1, -1]; Velocity: TPoint = [0, 0]): Boolean;
var
  B: TBox;
  Height: Int32;
begin
  if OurTile = Point(-1, -1) then
    OurTile := Self.RSW.GetMyPos();

  Height := Self.GetHeight(OurTile);

  // find the sink
  if not Self.FindSack(OurTile, Height, B) then
  begin
    Script.WriteMsg('ClickSack: sink not visible', True);
    exit();
  end;

  Script.WriteMsg('ClickSack: Found sink, checking uptext', True);

  Result := True;

  // now apply velocity and check uptext
  if Velocity <> Point(0, 0) then
  begin
    B := B.Offset(Script.GetOffsetForVel(B, Velocity));

    if not MainScreen.IsVisible(B.Middle()) then
    begin
      Script.WriteMsg('ClickSack: Tile velocity no visible', True);
      exit();
    end;

    B.LimitTo(MainScreen.Bounds());

    if Script.Debug and Script.Drawing then
      RSClient.Image.DrawBox(B, clMoneyGreen);
  end;

  if Velocity <> Point(0, 0) then
    Script.MouseLooksForUpText := Self.SackUptext;
  Mouse.Move(B);
  Script.MouseLooksForUpText := [];

  Script.RareSmallWait();

  if not MainScreen.IsUpText(Self.SackUptext, Random(100, 250)) then
  begin
    Script.WriteMsg('ClickSack: Bad uptext', True);
    exit();
  end;

  Script.WriteMsg('ClickSack: Good uptext, clicking', True);

  // uptext is the option we want
  if MainScreen.IsUpText(Self.SackOptions, Random(100, 250)) and (Random() < 0.9) then
  begin
    Mouse.Click(MOUSE_LEFT);
    WasClicked := Mouse.DidClick(True);
  end
  else if MainScreen.IsUpText('options') then
    WasClicked := ChooseOption.Select(Self.SackOptions);

  if not WasClicked then
  begin
    Script.WriteMsg('ClickSack: Didnt click', True);
    exit();
  end;

  Script.WriteMsg('ClickSack: Clicked', True);
end;

{
  While we walk to the sack
}
procedure WalkingToSack(Walker: PRSWalker; Position: TPoint; Destination: TPoint);
var
  WasClicked: Boolean;
  S: Int32;
  Velocity: TDoubleArray;
  MSVel: TPoint;
begin
  // calcuate move velocity
  Velocity := Script.GetVelocity(Position);

  // if this is the last node in the path
  if Destination = Walker^.Path[High(Walker^.Path)] then
  begin
    // make the mouse fast, because we are moving, its harder to keep up with objs
    S := Mouse.Speed;
    Mouse.Speed := S + Script.FastMouseAdd + Random(-2, 2);

    MSVel := Script.WalkerVelToMSVel(Velocity);

    // if we clicked the fish spot, then stop the walker
    if Bot.ClickSack(WasClicked, Position, MSVel) and WasClicked then
    begin
      Script.WriteMsg('Clicked the sack while moving, nice!');
      Walker^.Enabled := False;
    end;

    // reset the mosue speed back
    Mouse.Speed := S;
  end;
end;

{
  Walks to the sack, return true if we clicked it early
}
function TBot.GoToSack(Force: Boolean = False; Clicking: Boolean = True): Boolean;
var
  Walked: Boolean;
  OurPos: TPoint;
  BeforeCallback: TRSWalker_OnWalkEvent;
  B: TBox;
  Height: Int32;
begin
  Script.WriteMsg('Going to sack....', True);
  OurPos := Self.RSW.GetMyPos();
  Height := Self.GetHeight(OurPos);

  if Force or (OurPos.DistanceTo(Self.SackTile) > 50) or not Self.FindSack(OurPos, Height, B) then
  begin
    // mainscreen bank fix
    if Self.RSW.ScreenWalk and MainScreen.AnyInterfaceOpen() then
      MainScreen.CloseInterface(Script.ShouldPressKeyboard());

    Script.SetStatus('Going to sack...');

    BeforeCallback := @Self.RSW.OnWalkingEvent;
    if Clicking then
      Self.RSW.OnWalkingEvent := @WalkingToSack;
    try
      Walked := Self.RSW.WalkBlind(Self.SackTile.Random(-6, 6, True), 0);
    except
    end;
    Self.RSW.OnWalkingEvent := @BeforeCallback;

    if not Walked then
    begin
      Script.WriteMsg('Failed to go to sack!');
      MainScreen.CloseInterface(Script.ShouldPressKeyboard());
      exit();
    end;

    // check if the walker was killed early, it must of been it clicked the bank
    if not Self.RSW.Enabled then
      Result := True;
  end;
end;

{$ENDIF} // INEEDBOT_MLM_SACK
