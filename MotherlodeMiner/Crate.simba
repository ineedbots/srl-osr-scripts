{
  Motherload Miner crate
  Author: ineedbot
  Date: 8/27/2022

  Crate
}

{$IFNDEF INEEDBOT_MLM_CRATE}
{$DEFINE INEEDBOT_MLM_CRATE}

{$IFNDEF SRL_OSR}
  {$I SRL/OSR.simba}
{$ENDIF}

{$F-}

{$include_once ../Utils/Utils.simba}

{$include_once Types.simba}
{$include_once Utils.simba}

{
  Finds the Crate tile
}
function TBot.FindCrateTile(OurPos: TPoint; Height: Int32): TRectangle;
begin
  Result := Self.RSW.GetTileMSEx(OurPos, Self.CrateTile, Self.CrateHeight - Height);
end;

{
  Gets the search box
}
function TBot.GetCrateSearchBox(CrateTile: TRectangle): TBox;
begin
  Result := CrateTile.Expand(Round(CrateTile.Radius() * Self.CrateColor.RadiusExpand)).Bounds();
end;

{
  Returns all object colors within the box
}
function TBot.FindCrateColor(Area: TBox): T2DPointArray;
begin
  Result := MainScreen.FindObject(Self.CrateColor.RSObject, Area);
  Result.FilterSize(MainScreen.ConvertDistance(Self.CrateColor.FilterSize), __GT__);
end;

{
  Returns true if the Crate is visible, B is the bounds of the Crate on the screen
}
function TBot.FindCrate(OurTile: TPoint; Height: Int32; out B: TBox): Boolean;
var
  TileRect: TRectangle;
  ATPA: T2DPointArray;
begin
  TileRect := Self.FindCrateTile(OurTile, Height);

  B := Self.GetCrateSearchBox(TileRect);

  if not MainScreen.IsVisible(B.Middle()) then
    exit();

  Result := True;

  B.LimitTo(MainScreen.Bounds());

  ATPA := Self.FindCrateColor(B);

  if Length(ATPA) <= 0 then
    B := TileRect.Bounds().Expand(-MainScreen.ConvertDistance(Self.CrateColor.NoColorTileExpand)) // couldnt find color... use the fixd tile
  else
    B := ATPA.Biggest().Bounds();
end;

{
  Returns true if found the Crate, WasClicked true if clicked it
}
function TBot.ClickCrate(out WasClicked: Boolean; OurTile: TPoint = [-1, -1]; Velocity: TPoint = [0, 0]): Boolean;
var
  B: TBox;
  Height: Int32;
begin
  if OurTile = Point(-1, -1) then
    OurTile := Self.RSW.GetMyPos();

  Height := Self.GetHeight(OurTile);

  // find the sink
  if not Self.FindCrate(OurTile, Height, B) then
  begin
    Script.WriteMsg('ClickCrate: sink not visible', True);
    exit();
  end;

  Script.WriteMsg('ClickCrate: Found sink, checking uptext', True);

  Result := True;

  // now apply velocity and check uptext
  if Velocity <> Point(0, 0) then
  begin
    B := B.Offset(Script.GetOffsetForVel(B, Velocity));

    if not MainScreen.IsVisible(B.Middle()) then
    begin
      Script.WriteMsg('ClickCrate: Tile velocity no visible', True);
      exit();
    end;

    B.LimitTo(MainScreen.Bounds());

    if Script.Debug and Script.Drawing then
      RSClient.Image().DrawBox(B, clMoneyGreen);
  end;

  // uptext
  Script.OldMouseMovingCallback := Mouse.OnMoving;
  Mouse.OnMoving := WhileMouseMovesLookForUpText;
  if Velocity <> Point(0, 0) then
    Script.MouseLooksForUpText := Self.CrateUptext;

  Mouse.Move(B);

  Script.MouseLooksForUpText := [];
  Mouse.OnMoving := Script.OldMouseMovingCallback;
  Script.OldMouseMovingCallback := nil;
  

  Script.RareSmallWait();

  if not MainScreen.IsUpText(Self.CrateUptext, Random(100, 250)) then
  begin
    Script.WriteMsg('ClickCrate: Bad uptext', True);
    exit();
  end;

  Script.WriteMsg('ClickCrate: Good uptext, clicking', True);

  // uptext is the option we want
  if MainScreen.IsUpText(Self.CrateOptions, Random(100, 250)) and (Random() < 0.9) then
  begin
    Mouse.Click(MOUSE_LEFT);
    WasClicked := MainScreen.DidRedClick();
  end
  else if MainScreen.IsUpText('options') then
    WasClicked := ChooseOption.Select(Self.CrateOptions);

  if not WasClicked then
  begin
    Script.WriteMsg('ClickCrate: Didnt click', True);
    exit();
  end;

  Script.WriteMsg('ClickCrate: Clicked', True);
end;

{
  While we walk to the Crate
}
procedure WalkingToCrate(Walker: PRSWalker; Position: TPoint; Destination: TPoint);
var
  WasClicked: Boolean;
  S: Int32;
  Velocity: TDoubleArray;
  MSVel: TPoint;
begin
  if Bot.OldWalkingCallback <> nil then
  begin
    Bot.OldWalkingCallback(Walker, Position, Destination);

    if not Walker^.Enabled then
      exit();

    Position := Walker^.GetMyPos();
  end;

  // calcuate move velocity
  Velocity := Script.GetVelocity(Position);

  // if this is the last node in the path
  if Bot.ClickWhileWalking and (Destination = Walker^.Path[High(Walker^.Path)]) then
  begin
    // make the mouse fast, because we are moving, its harder to keep up with objs
    S := Mouse.Speed;
    Mouse.Speed := S + Script.FastMouseAdd + Random(-2, 2);

    MSVel := Script.WalkerVelToMSVel(Velocity);

    // if we clicked the fish spot, then stop the walker
    if Bot.ClickCrate(WasClicked, Position, MSVel) and WasClicked then
    begin
      Script.WriteMsg('Clicked the Crate while moving, nice!');
      Walker^.Enabled := False;
    end;

    // reset the mosue speed back
    Mouse.Speed := S;
  end;
end;

{
  Walks to the Crate, return true if we clicked it early
}
function TBot.GoToCrate(Force: Boolean = False; Clicking: Boolean = True): Boolean;
var
  Walked: Boolean;
  OurPos: TPoint;
  B: TBox;
  Height: Int32;
begin
  Script.WriteMsg('Going to Crate....', True);
  OurPos := Self.RSW.GetMyPos();
  Height := Self.GetHeight(OurPos);

  if Force or (OurPos.DistanceTo(Self.CrateTile) > 50) or not Self.FindCrate(OurPos, Height, B) then
  begin
    // mainscreen bank fix
    if Self.RSW.ScreenWalk and MainScreen.AnyInterfaceOpen() then
      MainScreen.CloseInterface(Script.ShouldPressKeyboard());

    Script.SetStatus('Going to Crate...');

    Self.OldWalkingCallback := Self.RSW.OnWalkingEvent;
    Self.ClickWhileWalking := Clicking;
    Self.RSW.OnWalkingEvent := WalkingToCrate;

    try
      Walked := Self.RSW.WalkBlind(Self.CrateTile.Random(-6, 6, True), 0);
    except
    end;
    
    Self.RSW.OnWalkingEvent := Self.OldWalkingCallback;
    Self.OldWalkingCallback := nil;

    if not Walked then
    begin
      Script.WriteMsg('Failed to go to Crate!');
      MainScreen.CloseInterface(Script.ShouldPressKeyboard());
      exit();
    end;

    // check if the walker was killed early, it must of been it clicked the bank
    if not Self.RSW.Enabled then
      Result := True;
  end;
end;

{$ENDIF} // INEEDBOT_MLM_CRATE
