{
  Wintertodt
  Author: ineedbot
  Date: 7/1/2022

  Brazier funcs
}

{$IFNDEF INEEDBOT_WINTERTODT_BRAZIER}
{$DEFINE INEEDBOT_WINTERTODT_BRAZIER}

{$IFNDEF SRL_OSR}
  {$I SRL/OSR.simba}
{$ENDIF}

{$include_once ../Utils/Utils.simba}

{$include_once Types.simba}
{$include_once Utils.simba}

{
  Find Brazier tile
}
function TBot.FindBrazierTile(OurPos: TPoint): TRectangle;
begin
  Result := Self.RSW.GetTileMSEx(OurPos, Self.BrazierTile);
end;

{
  Gets the search box
}
function TBot.GetBrazierSearchBox(TileRect: TRectangle): TBox;
begin
  Result := TileRect.Bounds().Expand(
    Round(TileRect.Radius() * Self.UnlitBrazColor.RadiusExpand));
end;

{
  Finds color
}
function TBot.FindLitBrazierColor(Area: TBox): T2DPointArray;
begin
  Result := MainScreen.FindObject(Self.LitBrazColor.RSObject, Area);
  Result.FilterSize(Self.LitBrazColor.FilterSize, __GT__);
end;

{
  Finds color
}
function TBot.FindUnlitBrazierColor(Area: TBox): T2DPointArray;
begin
  Result := MainScreen.FindObject(Self.UnlitBrazColor.RSObject, Area);
  Result.FilterSize(Self.UnlitBrazColor.FilterSize, __GT__);
end;

{
  Returns true if the brazier is visible
}
function TBot.FindBrazier(OurTile: TPoint; out IsLit: Boolean; out B: TBox): Boolean;
var
  TileRect: TRectangle;
  ATPA: T2DPointArray;
begin
  TileRect := Self.FindBrazierTile(OurTile);

  B := Self.GetBrazierSearchBox(TileRect);

  if not MainScreen.IsVisible(B.Middle()) then
    exit();

  Result := True;

  IsLit := False;

  B.LimitTo(MainScreen.Bounds());

  ATPA := Self.FindLitBrazierColor(B);

  if Length(ATPA) > 0 then
  begin
    B := ATPA.Biggest().Bounds();
    IsLit := True;
    exit();
  end;

  ATPA := Self.FindUnlitBrazierColor(B);

  if Length(ATPA) > 0 then
    B := ATPA.Biggest().Bounds()
  else
    B := TileRect.Bounds().Expand(-MainScreen.ConvertDistance(Self.UnlitBrazColor.NoColorTileExpand));
end;

{
  While offering to the brazier
}
procedure TBot.WhileOffering();
var
  OurHPPerc, HPP, ICount, IC: Int32;
  CountDown: TCountDown;
  FireItemSlots: TIntegerArray;
  _: TBox;
  IsLit: Boolean;
begin
  Script.WriteMsg('OfferLitBrazier: Walking to clicked braz', True);
  while RSClient.IsLoggedIn() and Minimap.HasFlag() and Minimap.IsPlayerMoving() do
    Self.DoMainLoopStuff('Going to clicked brazier', True);

  Script.WriteMsg('OfferLitBrazier: Watching for item changes', True);

  OurHPPerc := Minimap.GetHPPercent();
  Inventory.FindItems([Self.LogItem, Self.KnifedLogItem], FireItemSlots);
  ICount := Length(FireItemSlots);

  CountDown.Init(Self.BurnWaitTime);
  while RSClient.IsLoggedIn() do
  begin
    // check hp
    HPP := Minimap.GetHPPercent();
    if HPP < OurHPPerc then
    begin
      Script.WriteMsg('OfferLitBrazier: HP lowered...', True);
      break;
    end;
    OurHPPerc := HPP;

    // check inv
    FireItemSlots.Clear();
    Inventory.FindItems([Self.LogItem, Self.KnifedLogItem], FireItemSlots);
    IC := Length(FireItemSlots);

    if IC <= 0 then
    begin
      Script.WriteMsg('OfferLitBrazier: No more things to offer', True);
      break;
    end;

    if IC <> ICount then
    begin
      Script.WriteMsg('OfferLitBrazier: InvCount changed, restarting countdown', True);
      CountDown.Restart();
    end;

    ICount := IC;

    if CountDown.IsFinished() then
    begin
      Script.WriteMsg('OfferLitBrazier: Countdown expired', True);
      break;
    end;

    // check if still fired
    if not Self.FindBrazier(Self.RSW.GetMyPos(), IsLit, _) or not IsLit then
    begin
      Script.WriteMsg('OfferLitBrazier: No lit color found', True);
      break;
    end;

    // check levelup
    if Chat.LeveledUp() then
    begin
      Script.WriteMsg('OfferLitBrazier: Leveld', True);
      if Random() < 0.2 then
      begin
        Wait(0, 1500, wdLeft);
        Chat.ChatToOption('doesnt matter');
      end;
      break;
    end;

    Self.DoMainLoopStuff('Offering stuff to the brazier', True);
  end;
end;

{
  Fixes/Lights the unlit brazier
}
procedure TBot.WhileUnlitBrazier();
var
  IsLit: Boolean;
  _: TBox;
  CountDown: TCountDown;
  LastXP: Int32;
begin
  while RSClient.IsLoggedIn() and Minimap.HasFlag() and Minimap.IsPlayerMoving() do
    Self.DoMainLoopStuff('Going to clicked brazier', False);

  // TODO check pyro
  LastXP := XPBar.ReadXPBar();
  if LastXP = -1 then
  begin
    Wait(2500, 3000);
    exit();
  end;

  CountDown.Init(Self.UnlitWaitTime);
  while RSClient.IsLoggedIn() do
  begin
    if not Self.FindBrazier(Self.RSW.GetMyPos(), IsLit, _) then
      break;

    if IsLit then
      break;

    if LastXP <> XPBar.ReadXPBar() then
    begin
      Wait(0, 0);
      break;
    end;

    if CountDown.IsFinished() then
      break;

    Self.DoMainLoopStuff('Wait for xp change', False);
  end;
end;

{
  Returns true if found the brazier, WasClicked true if clicked it
}
function TBot.ClickBrazier(out WasClicked: Boolean; out IsLit: Boolean; OurTile: TPoint = [-1, -1]; Velocity: TPoint = [0, 0]): Boolean;
var
  B: TBox;
  UpTexts: TStringArray;
begin
  if OurTile = Point(-1, -1) then
    OurTile := Self.RSW.GetMyPos();

  if not Self.FindBrazier(OurTile, IsLit, B) then
  begin
    Script.WriteMsg('ClickBrazier: not visible', True);
    exit();
  end;

  // now apply velocity and check uptext
  if Velocity <> Point(0, 0) then
  begin
    B := B.Offset(Script.GetOffsetForVel(B, Velocity));

    if not MainScreen.IsVisible(B.Middle()) then
    begin
      Script.WriteMsg('ClickBrazier: Tile velocity no visible', True);
      exit();
    end;

    B.LimitTo(MainScreen.Bounds());

    if Script.Debug and Script.Drawing then
      RSClient.Image.DrawBox(B, clMoneyGreen);
  end;

  Result := True;
  UpTexts := Copy(Self.FixBrazUpText);
  UpTexts := UpTexts.Combine(Self.LightBrazUpText);
  UpTexts := UpTexts.Combine(Self.FeedBrazUpText);

  if Velocity <> Point(0, 0) then
    Script.MouseLooksForUpText := UpTexts;
  Mouse.Move(B);
  Script.MouseLooksForUpText := [];

  Script.RareSmallWait();

  if MainScreen.IsUpText(UpTexts, Random(100, 250)) and (Random() < 0.9) then
  begin
    Mouse.Click(MOUSE_LEFT);
    WasClicked := Mouse.DidClick(True);
  end
  else if MainScreen.IsUpText('options') then
    WasClicked := ChooseOption.Select(UpTexts);

  if not WasClicked then
  begin
    Script.WriteMsg('ClickBrazier: Didnt click', True);
    exit();
  end;
end;

{
  While we walk to the braz
}
procedure WalkingToBrazier(Walker: PRSWalker; Position: TPoint; Destination: TPoint);
var
  WasClicked, IsLit: Boolean = False;
  S: Int32;
  Velocity: TDoubleArray;
  MSVel: TPoint;
begin
  // calcuate move velocity
  Velocity := Script.GetVelocity(Position);

  // if this is the last node in the path
  if Destination = Walker^.Path[High(Walker^.Path)] then
  begin
    // make the mouse fast, because we are moving, its harder to keep up with objs
    S := Mouse.Speed;
    Mouse.Speed := S + Script.FastMouseAdd + Random(-2, 2);

    MSVel := Script.WalkerVelToMSVel(Velocity);

    if Bot.ClickBrazier(WasClicked, IsLit, Position, MSVel) and WasClicked then
    begin
      Bot.BrazIsLit := IsLit;
      Script.WriteMsg('Clicked the brazier while moving, nice!');
      Walker^.Enabled := False;
    end;

    // reset the mosue speed back
    Mouse.Speed := S;
  end
  else if Random() < 0.65 then
    Bot.EatFood(1); // eat food while walking

  // make sure outside inside arena
  if Bot.GetWinterTodtEnergyPerc() = -1 then
    Walker^.Enabled := False;
end;

{
  Gets an offset
}
function TBot.GetBrazierTileWalkOffset(): TPoint;
begin
  Result := Self.BrazierTileWalkOffsets[Self.BrazierTileWalkOffsetIndex];
end;

{
  Walks to the door, return true if we clicked it early
}
function TBot.GoToBrazier(Force: Boolean = False; DoClick: Boolean = True): Boolean;
var
  Walked: Boolean;
  PathIdx: Int32;
  OurPos: TPoint;
  BeforeCallback: TRSWalker_OnWalkEvent;
begin
  Script.WriteMsg('Going to brazier....', True);
  OurPos := Self.RSW.GetMyPos();

  if (OurPos.DistanceTo(Self.BrazierTile) > 30) or
    not MainScreen.IsVisible(Self.RSW.GetTileMSEx(OurPos, Self.BrazierTile).Mean()) or
    Force then
  begin
    Script.SetStatus('Going to brazier...');

    BeforeCallback := @Self.RSW.OnWalkingEvent;
    if DoClick then
      Self.RSW.OnWalkingEvent := @WalkingToBrazier;

    try
      PathIdx := SRL.TruncatedGauss(Low(Self.ToBrazierPaths), High(Self.ToBrazierPaths));

      if OurPos.DistanceTo(Self.ToBrazierPaths[PathIdx][Low(Self.ToBrazierPaths[PathIdx])]) <
        OurPos.DistanceTo(Self.ToBrazierPaths[PathIdx][High(Self.ToBrazierPaths[PathIdx])]) then
        Walked := Self.RSW.WalkPath(Self.ToBrazierPaths[PathIdx], 8)
      else
      begin
        Walked := True;
        Self.RSW.Enabled := True;
      end;

      if Walked and Self.RSW.Enabled then
        Walked := Self.RSW.WalkBlind(Self.BrazierTile.Offset(Self.GetBrazierTileWalkOffset()).Random(-6, 6, True), 0);
    except
    end;
    Self.RSW.OnWalkingEvent := @BeforeCallback;

    if not Walked then
    begin
      Script.WriteMsg('Failed to go to brazier!');
      exit();
    end;

    // check if the walker was killed early, it must of been it clicked
    if not Self.RSW.Enabled then
      Result := True;
  end;
end;

{
  Does interactions with the brazier
}
procedure TBot.OfferStuffToBrazier();
var
  DidClick, IsLit: Boolean;
begin
  Script.WriteMsg('Offering to the brazier...', True);

  if (Self.ClickBrazier(DidClick, IsLit) and DidClick) or
    Self.GoToBrazier() or
    (Self.ClickBrazier(DidClick, IsLit) and DidClick) or
    Self.GoToBrazier(True) or
    (Self.ClickBrazier(DidClick, IsLit) and DidClick) then
  begin
    if DidClick then
      Self.BrazIsLit := IsLit;

    if Self.BrazIsLit then
    begin
      Self.AfterActionAntiBan(True);
      Self.WhileOffering();
    end
    else
      Self.WhileUnlitBrazier();
  end
  else
    Self.DoAntiBan();
end;

{
  Waits for the boss to start
}
procedure TBot.WaitForBossStart();
var
  TimeLeftText: String;
  DidClick, WasLit: Boolean;
begin
  Script.WriteMsg('Waitting for boss start...', True);

  Self.DoAntiBan();

  Self.GoToBrazier(True, False);

  if Self.GetWinterTodtEnergyPerc() = 0 then
  begin
    while RSClient.IsLoggedIn() do
    begin
      TimeLeftText := Self.GetWinterTodtRemainingTime();
      if (Self.GetWinterTodtEnergyPerc() <> 0) or
        TimeLeftText.EndsWith('0:00') then
        break;

      if TimeLeftText.EndsWith('0:01') or TimeLeftText.EndsWith('0:02') or
        TimeLeftText.EndsWith('0:03') or TimeLeftText.EndsWith('0:04') then
      begin
        Mouse.Move(Self.RSW.GetTileMS(Self.BrazierTile).Bounds());
        Self.DoMainLoopStuff('Waiting for boss to start', False);
      end
      else
      begin
        Self.DoMainLoopStuff('Waiting for boss to start', True);

        if Random() < 0.65 then
          Self.EatFood(1);
      end;
    end;

    if Self.ClickBrazier(DidClick, WasLit) and DidClick then
      Self.WhileUnlitBrazier();
  end;
end;

{$ENDIF} // INEEDBOT_WINTERTODT_BRAZIER

